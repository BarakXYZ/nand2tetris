/*
Opening animation of the game and difficulty menu selection
*/

class Animations {
    function void start() {    
        var char kp;    
        
        do Animations.openScreen();
        do Game.setUpGame(Animations.difficultyMenu());

        while (true) {
            do Game.startGame();

            do Output.moveCursor(21, 19);
            do Main.printStringConst("Press `space` to play again");

            let kp = 0;
            while (~(kp = 32)) {
                let kp = Keyboard.keyPressed();
            }

            do Game.setDifficulty(Animations.difficultyMenu());
        }


        return;
    }

    // Opening animation of the game
    function void openScreen() {
        var char kp;
        var int i;
        var int count;
        var String gameName;
        var String s;
        let gameName = "MEMORY GAME";
        let s = "Press any key to start...";

        do Screen.clearScreen();
        do Output.moveCursor(11, 26);

        let i = 0;
        while (i < gameName.length()) {
            do Sys.wait(20);
            do Output.printChar(gameName.charAt(i));
            let i = i + 1;
        }

        do Output.moveCursor(21, 19);

		let i = 0;
		while (i < s.length()) {
			do Output.printChar(s.charAt(i));
			let i = i + 1;
		}

        let kp = Keyboard.keyPressed();
        let count = 0;
		while (kp = 0) {
			let kp = Keyboard.keyPressed();
            let count = count + 1;
		}

        do Random.setSeed(count);     // Setting seed according to input delay
        do Screen.clearScreen();
        do gameName.dispose();
        
        return;
    }

    // Menu of the game
    function int difficultyMenu() {
        var int i;
        var char kp;
        var String s;
        let s = "Press the number key for difficulty...";

        do Screen.clearScreen();
        do Output.moveCursor(3,22);
        do Main.printStringConst("SELECT DIFFICULTY");
        
        do Output.moveCursor(7, 18);
        do Main.printStringConst("1) EASY (only alphabets)");

        do Output.moveCursor(9, 18);
        do Main.printStringConst("2) MEDIUM (alphanumeric)");

        do Output.moveCursor(11, 18);
        do Main.printStringConst("3) HARD (any character)");

        do Output.moveCursor(21, 15);
        
        let i = 0;
		while (i < s.length()) {
			do Output.printChar(s.charAt(i));
			let i = i + 1;
		}

		while (true) {
			let kp = Keyboard.keyPressed();

            if (kp = 49) { return 26; }
            if (kp = 50) { return 36; }
            if (kp = 51) { return 54; }
		}

        return 0;
    }
}
