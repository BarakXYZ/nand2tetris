class Challenge {

        static int maxLen, row, col;
        static String selBuff, tempBuff;
        static Array buffArr, trackBuffArr;
        static int selBuffIndex, groupBuffIndex;
        static Array answers;
        static boolean isArrow, isEnter;
        static ChallengeBuffer b0, b1, b2, b3, b4, b5;
        static ChallengeBuffer currentBuffer;
        static Array bufferArr;
        static int buffArrSize;

        function void init() {
            do Challenge.initBuffers();
            let row = 5;
            let col = 11;
            let selBuff = currentBuffer.getBufferAt(13);  // default buffer
            let tempBuff = currentBuffer.getBufferAt(13);  // default buffer
            let maxLen = currentBuffer.getMaxStrLen() - 1;

            let selBuffIndex = 13;

            let isArrow = false;
            let isEnter = false;

            do Challenge.initAnswers();
            return;
        }

        function void initAnswers() {
            let buffArrSize = 5;
            let answers = Array.new(buffArrSize);
            
            let answers[0] = "6";
            let answers[1] = "555";
            let answers[2] = "99999";

            return;
        }

        function void initBuffers() {
            let b0 = ChallengeBuffer.new();
            let b1 = ChallengeBuffer.new();
            let b2 = ChallengeBuffer.new();
            let b3 = ChallengeBuffer.new();
            let b4 = ChallengeBuffer.new();
            let b5 = ChallengeBuffer.new();

            let bufferArr = Array.new(10);
            let bufferArr[0] = b0;
            let bufferArr[1] = b1;
            let bufferArr[2] = b2;
            let bufferArr[3] = b3;
            let bufferArr[4] = b4;
            let bufferArr[5] = b5;

            let currentBuffer = b0;
            let groupBuffIndex = 0;  // default Shimon group
            return;
        }

        
        //** if 'A' is pressed, enter '[A]nswer mode' */
        function void run(char c) {
            do UserInterface.drawInstruction(0);
            // do UserInterface.drawSelArrows(UserInterface.getSelCoordAnswer());
            do UserInterface.drawChallengeSel(selBuffIndex , true);
            do Sys.wait(200);

            // Exits if 'Escape' (140) is pressed
            while (~(c = 140)) {
                do Draw.sprite();
                do Challenge.resetParams();

                do Input.manageCharAndTimeBuffer(c);
                let c = Keyboard.keyPressed();

                // if 'Enter': insert answer and exit 'Answer Mode'
                if (Input.isEnter(c)) {
                    if (NPC.checkChallengeAnswer(currentBuffer[13], currentBuffer[14])) {
                        let c = 140;
                        // check if need to rearrange UI a bit (we need to!)
                        // Fix asterisk on Challenge pane when it summoned
                        // Fix the UI of the content (it slips data)
                        // Lookup functionality:
                            // draw new sprites and figure out the limitations - X
                            // Draw the correct challenge on visit - X

                        // Plot twist and an interesting challenge
                        // master challenge and wrap up
                    }

                    let isEnter = true;
                }

                if (c = 131) { do Challenge.upArrow(); }
                if (c = 133) { do Challenge.downArrow(); }

                // Append / Erase char if non-zero & no Enter & no Escape & no Arrow
                if ((~(c = 0)) & (~(isEnter)) & (~(isArrow)) & (~(c = 140))) {
                    do Debug.key(c);
                    do Debug.event();
                    
                    // is erase? (129 == backspace)
                    if ((selBuff.length() > 0) & (c = 129)) { do Challenge.eraseChar(); }
                    else {  // is append
                        if ((selBuff.length() < maxLen) & (~(c = 129))) {
                            do Challenge.appendChar(c); 
                        }
                    }
                }

            }
            do UserInterface.drawChallengeSel(selBuffIndex , false);
            do UserInterface.clearInstruction();
            return;
        }

        function void resetParams() {
            let isArrow = false;
            let isEnter = false;
            return;
        }

        //** Append Characater:
        // Prints the updated string to the given row & col coords */
        function void appendChar(char c) {
            do selBuff.appendChar(c);  // built-in String method
            do Output.moveCursor(row + selBuffIndex, col);
            do Output.printString(selBuff);
            return;
        }

        //** Erase Character:
        // Prints the updated string to the given row & col coords */
        function void eraseChar() {
            // Clean old screen buffer (last char)
            do selBuff.appendChar(String.backSpace());
            do Output.moveCursor(row + selBuffIndex, col);
            do Output.printString(selBuff);

            do selBuff.eraseLastChar();  // erase backspace
            do selBuff.eraseLastChar();  // erase actual char
            return;
        }

        function void upArrow() {
            let isArrow = true;
            do UserInterface.drawChallengeSel(selBuffIndex, false);
            if (selBuffIndex = 0) { let selBuffIndex = 14; }
            else { 
                if (selBuffIndex = 13) { let selBuffIndex = 11; }
                else { let selBuffIndex = selBuffIndex - 1; } 
            }
            do Challenge.manageBuffer();
            return;
        }

        function void downArrow() {
            let isArrow = true;
            do UserInterface.drawChallengeSel(selBuffIndex, false);
            if (selBuffIndex = 14) { let selBuffIndex = 0; }
            else { 
                if (selBuffIndex = 11) { let selBuffIndex = 13; }
                else { let selBuffIndex = selBuffIndex + 1; } 
            }
            do Challenge.manageBuffer();
            return;
        }

        function void manageBuffer() {
            let selBuff = currentBuffer.getBufferAt(selBuffIndex);
            do UserInterface.drawChallengeSel(selBuffIndex, true);
            return;
        }
        
        function void resetBuffer() {
            do currentBuffer.dispose();
            let currentBuffer = ChallengeBuffer.new();
            let buffArr[groupBuffIndex] = currentBuffer;  // maybe reset all buffers
            do UserInterface.drawChallengeSel(selBuffIndex, false);
            return;
        } 

        function void setSelBuffIndex(int aIndex) {
            let selBuffIndex = aIndex;
            return;
        }

        function void setCurrentBuffer(int aIndex) {
            let groupBuffIndex = aIndex;
            let currentBuffer = bufferArr[groupBuffIndex];
            do Challenge.manageBuffer();
            return;
        }

        function void drawBuffer() {
            do currentBuffer.draw();
            do UserInterface.drawChallengeFrame();
            return;
        }
}
