class Lookup {
        
        static int maxLen, row, col;
        static String lookupInput, lookupTemp;

        function void init() {

            let maxLen = 6;
            let row = 21;
            let col = 30;
            let lookupTemp = String.new(7);
            let lookupInput = String.new(7);

            return;
        }

        function void run(char c) {
            // max address == 24,576

            do UserInterface.drawInstruction(0);
            do UserInterface.drawSelArrows(UserInterface.getSelCoordLookUp());
            do Sys.wait(200);

            // Exits if 'Escape' (140) is pressed
            while (~(c = 140)) {
                do Draw.sprite();

                do Input.manageCharAndTimeBuffer(c);
                let c = Keyboard.keyPressed();

                // if 'Enter': Lookup Address & exit Lookup Mode
                if (Input.isEnter(c)) {
                    do Lookup.checkAddress();
                    let c = 140;
                }


                // Append or Erase char if non-zero & no Escape (140)
                if ((~(c = 0)) & (~(c = 140))) {
                    do Debug.key(c);
                    do Debug.event();
                    
                    // is erase? (129 == backspace)
                    if ((lookupInput.length() > 0) & (c = 129)) { 
                        do Lookup.eraseChar();
                    }
                    else {  // is append
                        if ((lookupInput.length() < (maxLen)) & (~(c = 129))) {
                            do Lookup.appendChar(c); 
                        }
                    }
                }


            }
            do UserInterface.eraseSelArrows(UserInterface.getSelCoordLookUp());
            do UserInterface.clearInstruction();
            return;
        }

        function void checkAddress() {
            var int activeQuest;
            let activeQuest = QuestManager.getActive();

            if (Helpers.strComp(lookupInput, "SHIMON") | Helpers.strComp(lookupInput, "555")) {
                do Lookup.new(0);
                return;
            }
            if (Helpers.strComp(lookupInput, "GNIRUT") | Helpers.strComp(lookupInput, "357")) {
                do Lookup.new(1);
                return;
            }
            return;
        }

        function void new(int aIndex) {
            do UserInterface.clearChallengeAll();
            do UserInterface.clearContentFrame();
            do NPC.setActive(aIndex);
            do NPC.setChallengeNode(aIndex);
            do NPC.setContentNode(aIndex);
            do Challenge.setCurrentBuffer(aIndex);
            do Challenge.drawBuffer();
            
            do Game.setNewLookup(true);

            return;
        }

        //** Append Characater:
        // Prints the updated string to the given row & col coords */
        function void appendChar(char c) {
            do lookupInput.appendChar(c);  // built-in String method
            do Output.moveCursor(row, col);
            do Output.printString(lookupInput);
            return;
        }

        //** Erase Character:
        // Prints the updated string to the given row & col coords */
        function void eraseChar() {
            // Clean old screen buffer (last char)
            do lookupInput.appendChar(String.backSpace());
            do Output.moveCursor(row, col);
            do Output.printString(lookupInput);

            do lookupInput.eraseLastChar();  // erase backspace
            do lookupInput.eraseLastChar();  // erase actual char
            return;
        }
}
